<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25" zv="Cache for Windows (x86-64) 2015.1.1 (Build 505U)" ts="2016-02-19 21:01:31">
<Class name="WebTerminal.Installer">
<Description>
Importing this class will install Cache WEB Terminal properly.</Description>
<Super>%Projection.AbstractProjection</Super>
<TimeChanged>63967,75686.361916</TimeChanged>
<TimeCreated>63890,71053.144208</TimeCreated>
<DependsOn>Common,Engine,Router,StaticContent,Autocomplete,Trace</DependsOn>

<Parameter name="DispatchClass">
<Default>WebTerminal.Router</Default>
</Parameter>

<Projection name="Reference">
<Type>Installer</Type>
</Projection>

<Method name="RegisterWebApplication">
<ClassMethod>1</ClassMethod>
<FormalSpec>name:%String,spec</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    new $Namespace
    set $Namespace = "%SYS"
    set st = $$$OK
    if ('##class(Security.Applications).Exists(name)) {
        write !,"Creating WEB application """_name_"""..."
        set st = ##class(Security.Applications).Create(name, .spec)
        write !, "WEB application """_name_""" is created."
    } else {
        do ##class(Security.Applications).Get(name, .props)
        if (props("DispatchClass") '= ..#DispatchClass) && (name = "/terminal") {
            write !, "WARNING! WEB application """_name_""" exists but does not refer to ",
                ..#DispatchClass, ".", !,
                "Please, set correct dispatch class for this application ",
                "or create a terminal WEB-application manually."
        } else {
            write !, "WEB application """_name_""" already exists, so it should be ready to use. If not - remove this application manually and recompile."
        }
    }
    return st
]]></Implementation>
</Method>

<Method name="RemoveWebApplication">
<ClassMethod>1</ClassMethod>
<FormalSpec>name:%String</FormalSpec>
<Implementation><![CDATA[
    new $Namespace
    set $Namespace = "%SYS"
    if (##class(Security.Applications).Exists(name)) {
        do ##class(Security.Applications).Get(name, .props)
        if (props("DispatchClass") '= ..#DispatchClass) && (name = "/terminal") {
            write !, "Won't delete WEB-application """_name_""" because it does not refer to dispatch class anymore."
        } else {
            write !, "Deleting WEB application """_name_"""..."
            do ##class(Security.Applications).Delete(name)
            write !, "WEB application """_name_""" was successfully deleted."
        }
    }
]]></Implementation>
</Method>

<Method name="CreateProjection">
<Description>
This method is invoked when a class is compiled.</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[cls:%String,&params]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	new $Namespace
	set ns = $Namespace // ought to be package home namespace!
    set ^WebTerminal("HomeNamespace") = ns
    write !, "Installing WebTerminal application to " _ ns
    set dbdir = $$$defdir
    set $Namespace = "%SYS"
    
    set cspProperties("AutheEnabled") = $$$AutheCache
    set cspProperties("NameSpace") = ns
    set cspProperties("Description") = "A WEB application for Cache WEB Terminal."
    set cspProperties("IsNameSpaceDefault") = $$$NO
    set cspProperties("DispatchClass") = ..#DispatchClass
    set st = ..RegisterWebApplication("/terminal", .cspProperties)
    quit:$$$ISERR(st) st
    
    set cspProperties("AutheEnabled") = $$$AutheUnauthenticated
    set cspProperties("Description") = "An application representing the open socket for /terminal application. Required since 2016.* Cache versions changed WebSocket behavior. PLEASE LEAVE THIS APPLICATION UNAUTHENTICATED, but do not worry: the socket won't accept anyone without the key."
    set cspProperties("DispatchClass") = ""
    set role = ..GetDBRole(dbdir)
    set cspProperties("MatchRoles")=":%DB_CACHESYS" _ $case($get(role)'="", 1: ":"_role, :"")
    set st = ..RegisterWebApplication("/terminalsocket", .cspProperties)
    quit:$$$ISERR(st) st
    
    set st = ..CreateAllNamespace()
    if ($$$ISERR(st)) {
        do $System.Status.DisplayError(st)
    } else {
        write !, "%All namespace is created."
    }
    
    write !, "Mapping %WebTerminal package into all namespaces:"
    set st = ..Map(ns)
    if ($$$ISERR(st)) {
        do $System.Status.DisplayError(st)
    } else {
	    write !, "Mapping complete."
    }
    write !, "WebTerminal package successfully mapped into all namespaces."
    quit st
]]></Implementation>
</Method>

<Method name="RemoveProjection">
<Description>
This method is invoked when a class is 'uncompiled'.</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[cls:%String,&params,recompile:%Boolean]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	new $Namespace
	
	set ns = $get(^WebTerminal("HomeNamespace"))
    set msg = "Please, set ^WebTerminal(""HomeNamespace"") global equal to " _
              "the namespace WebTerminal was installed to before uninstalling." _ $Namespace
    quit:'ns $$$ERROR($$$GeneralError, msg)
    write !, "Uninstalling WebTerminal application from "
    zn "%SYS"
    do ..RemoveWebApplication("/terminal")
    do ..RemoveWebApplication("/terminalsocket")
    
    write !, "Unmapping %WebTerminal package from all namespaces:"
	set st = ..UnMap(ns)
    if ($$$ISERR(st)) {
        do $System.Status.DisplayError(st)
    } else {
	    write !, "Unmapping complete."
    }
    
    kill:st ^WebTerminal
    write !, "Global ^WebTerminal removed."
    quit st
]]></Implementation>
</Method>

<Method name="GetDBRole">
<ClassMethod>1</ClassMethod>
<FormalSpec>directory:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	quit:'$d(directory) ""
	new $Namespace
	set $Namespace = "%SYS"
	#dim db As SYS.Database
	set db=##class(SYS.Database).%OpenId(directory)
	if $Isobject(db) {
		set resource = db.ResourceName
		set role = resource // I'm assuming that default role exists
	} else {
		set role = ""
	}
	quit role
]]></Implementation>
</Method>

<Method name="CreateAllNamespace">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	new $Namespace
    set $Namespace = "%SYS"
	set ns = "%All"
	set st = $$$OK
	if ('##Class(Config.Namespaces).Exists(ns)) {
		set Properties("Globals") = "CACHETEMP"
		set Properties("Library") = "CACHELIB"
		set Properties("Routines") = "CACHETEMP"
		set Properties("SysGlobals") = "CACHESYS"
		set Properties("SysRoutines") = "CACHESYS"
		set Properties("TempGlobals") = "CACHETEMP"
		set st = ##Class(Config.Namespaces).Create(ns, .Properties)
	}
	return st
]]></Implementation>
</Method>

<Method name="Map">
<ClassMethod>1</ClassMethod>
<FormalSpec>fromNS=""</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	new $Namespace
    set $Namespace = "%SYS"  
    set st = $$$OK

    set mapTo = $LISTBUILD("%All", "SAMPLES", "DOCBOOK")
    do ##Class(Config.Namespaces).Get(fromNS, .InstallNSProps)
    set Properties("Database") = $get(InstallNSProps("Routines"))
    set ptr = 0
    while $LISTNEXT(mapTo, ptr, namespace) {
        continue:(fromNS = namespace)
        write " ", namespace
        if ('##Class(Config.MapPackages).Exists(namespace, "WebTerminal")) {
        	set st1 = ##Class(Config.MapPackages).Create(namespace, "WebTerminal", .Properties)
        }
        if ('##Class(Config.MapGlobals).Exists(namespace, "WebTerminal")) {
	        set st2 = ##Class(Config.MapGlobals).Create(namespace, "WebTerminal", .Properties)
        }
        set st = $$$ADDSC(st,$$$ADDSC($get(st1,$$$OK),$get(st2,$$$OK)))
    }
    quit st
]]></Implementation>
</Method>

<Method name="UnMap">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	new $Namespace
    set $Namespace = "%SYS"
    set st = $$$OK
    
	set mapTo = $LISTBUILD("%All", "SAMPLES", "DOCBOOK")
    set ptr = 0
    while $LISTNEXT(mapTo, ptr, namespace) {
        write " ", namespace
        if (##Class(Config.MapPackages).Exists(namespace, "WebTerminal")) {
        	set st1 = ##Class(Config.MapPackages).Delete(namespace, "WebTerminal", .Properties)
        }
        if (##Class(Config.MapGlobals).Exists(namespace, "WebTerminal")) {
	        set st2 = ##Class(Config.MapGlobals).Delete(namespace, "WebTerminal", .Properties)
        }
        set st = $$$ADDSC(st,$$$ADDSC($get(st1,$$$OK),$get(st2,$$$OK)))
    }
    quit st
]]></Implementation>
</Method>
</Class>
</Export>
