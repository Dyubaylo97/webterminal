<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="%WebTerminal.Engine">
<Description>
This class represents the core of web terminal.
All operations with opened WebSocket placed here.
CLASS USES GLOBAL ^%WebTerminal.Engine.AuthorizationKey for client
authorization.
(Maybe there can be something better then using this global)</Description>
<Super>%CSP.WebSocket,%Library.Routine,%CSP.REST</Super>
<TimeCreated>63047,60359.445979</TimeCreated>

<Property name="lastClientId">
<Description>
Last joined client id</Description>
<Type>%Numeric</Type>
<InitialExpression>0</InitialExpression>
</Property>

<Property name="ConstServerActionExecute">
<Type>%Char</Type>
<InitialExpression>$c(1)</InitialExpression>
</Property>

<Property name="ConstServerActionExecuteSQL">
<Type>%Char</Type>
<InitialExpression>$c(2)</InitialExpression>
</Property>

<Property name="ConstServerActionGenerateAutocomplete">
<Type>%Char</Type>
<InitialExpression>$c(3)</InitialExpression>
</Property>

<Property name="ConstClientEnterClearIO">
<Type>%Char</Type>
<InitialExpression>$c(1)</InitialExpression>
</Property>

<Property name="ConstClientExitClearIO">
<Type>%Char</Type>
<InitialExpression>$c(2)</InitialExpression>
</Property>

<Property name="ConstClientOutputMessage">
<Type>%Char</Type>
<InitialExpression>$c(3)</InitialExpression>
</Property>

<Property name="ConstClientChangeNamespace">
<Type>%Char</Type>
<InitialExpression>$c(4)</InitialExpression>
</Property>

<Property name="ConstClientLoadAutocomplete">
<Type>%Char</Type>
<InitialExpression>$c(5)</InitialExpression>
</Property>

<Property name="ConstClientReadString">
<Type>%Char</Type>
<InitialExpression>$c(6)</InitialExpression>
</Property>

<Property name="ConstClientReadChar">
<Type>%Char</Type>
<InitialExpression>$c(7)</InitialExpression>
</Property>

<Property name="CurrentNamespace">
<Type>%String</Type>
</Property>

<Property name="InitialZName">
<Type>%String</Type>
</Property>

<Property name="ExperimentalAutocompleteOn">
<Type>%Boolean</Type>
<InitialExpression>0</InitialExpression>
</Property>

<Method name="redirects">
<Description>
Public point entries, using when redirecting i/o</Description>
<Internal>1</Internal>
<Private>1</Private>
<ProcedureBlock>0</ProcedureBlock>
<Implementation><![CDATA[
readFromSocket(len,timeout,sharedConnection)
	set sc = ""
	Set $ZTrap="ReadError"
	If len<1 Set len=32656
	If (sharedConnection = 1) {
		Set data=..ReadAsync(.len, .sc, timeout)
		Try { Set data1=$zconvert(data,"I","UTF8") } Catch { Set data1=data }
		Set len=$Length(data1)
		Quit data1
	}
	If ..BytesAvailable Goto ReadData
	Set ..BytesAvailable=0,..AtEnd=0
	Read header#5:timeout
	If $Test=0 {
		Set len=0
		Set sc=$$$ERROR($$$CSPWebSocketTimeout,"WebSocket Read Operation timed-out")
		Set ..AtEnd=1
		Quit ""
	}
	Set type=$Extract(header,5)
	If type=9 {
		Set len=0
		Set sc=$$$ERROR($$$CSPWebSocketClosed,"WebSocket Closed by the Client")
		Set ..AtEnd=1
		Quit ""
	}
	Set header=$Extract(header, 1, 4)
	Set size=$ZLA(header)-1 If size<0 Set size=0
	Set ..BytesAvailable=size
ReadData
	If ..BytesAvailable > len {
		Set size=len
		Set ..BytesAvailable=..BytesAvailable-len
	}
	else {
		Set size=..BytesAvailable
		Set ..BytesAvailable=0
	}
	If '..BytesAvailable Set ..AtEnd=1
	If size=0 Set len=0,sc=$$$OK Quit ""
	Read data#size
	Set len=size
	Set sc=$$$OK
	Try { Set data1=$zconvert(data,"I","UTF8") } Catch { Set data1=data }
	Set len=$Length(data1)
	Quit data1
ReadError	
	#; No interrupts during cleanup or error processing
	$$$SetExternalInterrupts(0)
	Set $ZTrap="ReadHalt"
	Hang 5
	Close 0
ReadHalt 
	Halt
	
PrepareOutput(data)
	Try { Set data1=$zconvert(data,"O","UTF8") } Catch { Set data1=data }
	quit data1
	
PrepareInput(data)
	Try { Set data1=$zconvert(data,"I","UTF8") } Catch { Set data1=data }
	quit data1
	
//////////////////////////////////////////////
	
wstr(s)
	set rd = ##class(%Device).ReDirectIO($$$NO) 
	//do ..Write(s)
	w $$PrepareOutput(s),*-3
	do ##class(%Device).ReDirectIO(rd)
	quit 
wchr(c) 
	set rd = ##class(%Device).ReDirectIO($$$NO) 
	w $$PrepareOutput($c(s)),*-3
	//do ..Write($c(c))
	do ##class(%Device).ReDirectIO(rd) 
	quit
wnl 
	set rd = ##class(%Device).ReDirectIO($$$NO) 
	//do ..Write($c(13,10))
	w $c(13,10),*-3
	do ##class(%Device).ReDirectIO(rd)
	quit 
wff 
	set rd = ##class(%Device).ReDirectIO($$$NO) 
	w $c(12),*-3
	//do ..Write($c(12))
	do ##class(%Device).ReDirectIO(rd)
	quit 
wtab(s)
	set rd = ##class(%Device).ReDirectIO($$$NO) 
	w $$PrepareOutput($j("",s-$x)),*-3
	//do ..Write($j("",s-$x))
	do ##class(%Device).ReDirectIO(rd) 
	quit
	
rstr(len = 32656,timeout = 86400)
	do ..ClientAlertNamespace()
	set rd=##class(%Device).ReDirectIO($$$NO)
	w ..ConstClientReadString,*-3
	read header#5:timeout
	read data:timeout
	set data = $$PrepareInput($EXTRACT(data,1,len))
	do ##class(%Device).ReDirectIO(rd)
	quit data
rchr(timeout = 86400)
	do ..ClientAlertNamespace()
	set rd=##class(%Device).ReDirectIO($$$NO)
	w ..ConstClientReadChar,*-3
	read header#5:timeout
	read data:timeout
	set data = $$PrepareInput($ASCII($EXTRACT(data,1,1)))
	do ##class(%Device).ReDirectIO(rd)
	quit data
]]></Implementation>
</Method>

<Method name="OnPreServer">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	quit $$$OK
]]></Implementation>
</Method>

<Method name="Debug">
<ClassMethod>1</ClassMethod>
<FormalSpec>data:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[	set ^debug($I(^debug)) = $ZTIMESTAMP _ ": " _ data
]]></Implementation>
</Method>

<Method name="OnPostServer">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[ 	quit $$$OK
]]></Implementation>
</Method>

<Method name="GetJSAutocompleteFilePath">
<FormalSpec>namespace:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	
	set data = ##class(%Library.File).ManagerDirectory()
	set data = ##class(%Library.File).ParentDirectoryName(data)
	set data = data _ "CSP\sys\webTerminal\js\autocomplete\" _
		$REPLACE(namespace,"%","_") _ ".js" // UNIX?
	quit data
]]></Implementation>
</Method>

<Method name="WriteToFile">
<FormalSpec>filename:%String,data:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set file=##class(%File).%New(filename)
	do file.Open("WSN")
	do file.WriteLine(data)
	do file.Close()
]]></Implementation>
</Method>

<Method name="GenerateAutocompleteFile">
<FormalSpec>namespace:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	set filename = ..GetJSAutocompleteFilePath($NAMESPACE)
	
	do ..StartClearIO()
	
	if ('##class(%SYS.Namespace).Exists($REPLACE(namespace,"_","%"))) {
		do ..EndClearIO()
		do ..SendData("Wrong namespace name: " _ namespace)
		quit $$$NOTOK
	}
	
	set namespace = $REPLACE(namespace,"%","_")
	
	if (##class(%File).Exists(filename)) {
		
		do ..EndClearIO()
		do ..SendData(namespace, ..ConstClientLoadAutocomplete)
		quit $$$OK
		
	} elseif ('##class(%File).DirectoryExists(##class(%File).GetDirectory(filename))) {
		
		// try to create missed directory
		set result = ##class(%File).CreateDirectoryChain(##class(%File).GetDirectory(filename))
		
		if (result '= 1) {
			do ..EndClearIO()
			do ..SendData("Autocomplete fail: can't create directory js/autocomplete.")
			quit $$$OK
		}
		
		set filename = ..GetJSAutocompleteFilePath(namespace)
		
	}

	do ..SendData("Generating language file for " _ namespace _ ", pleace, wait.")
	
	// write empty file in case of other terminal applications may require it
	do ..WriteToFile(filename,"{}")
	
	// get all classes names
	set result = ##class(%ResultSet).%New("%Dictionary.ClassDefinition:Summary")
	do result.Execute()
	
	/*	
	The next COULD BE IMPROVED FOR SPEED, I beleive.
	Generates compressed JSON string of type:
	{
		"%ClassName1": {
			"methodName": 0,
			"propertyName": 0,
			"parameterName": 0,
			...
		},
		"ClassName2": {
			...
		}
	}
	*/
	
	// final data generation
	set data = "{"
	set first = ""
	
	while (result.Next()) { // forming autocomplete for each class
	
		set className = result.Data("Name")
		set data = data _ first _ """" _ className _ """:{"
		if (first = "") set first = ","
		
		set cdefs = ##class(%Dictionary.ClassDefinition).%OpenId(className)
 		
 		set countMethods = cdefs.Methods.Count()
 		set countParameters = cdefs.Parameters.Count()
 		set countProperties = cdefs.Properties.Count()
 		set total = countMethods + countParameters + countProperties
 		set current = 0
 		
 		for i=1:1:countMethods {
	 		set current = current + 1
     		set data = data _ """" _cdefs.Methods.GetAt(i).Name _ """:0"
     		if (current'=total) set data = data _ ","
 		}
 		
 		for i=1:1:countProperties {
	 		set current = current + 1
     		set data = data _ """" _cdefs.Properties.GetAt(i).Name _ """:0"
     		if (current'=total) set data = data _ ","
 		}
 		
 		for i=1:1:countParameters {
	 		set current = current + 1
     		set data = data _ """" _cdefs.Parameters.GetAt(i).Name _ """:0"
     		if (current'=total) set data = data _ ","
 		}
		
		set data = data _ "}"
		
	}
	
	set data = data _ "}"
	
	do ..WriteToFile(filename,data)
	
	do ..SendData($c(10)_"Language file for " _ namespace _ " classes created.")
	
	do ..EndClearIO()
	
	do ..SendData(namespace,..ConstClientLoadAutocomplete)
	
	quit $$$OK
]]></Implementation>
</Method>

<Method name="GenerateAuthKey">
<Description>
Generating new authorization key</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	set temp = $SYSTEM.Util.CreateGUID()
	set ^%WebTerminal.Engine.AuthorizationKey = temp
]]></Implementation>
</Method>

<Method name="GetAuthKey">
<Description>
Returns key for client authorization.
If the key is empty, generates new key value.</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	if ($get(^%WebTerminal.Engine.AuthorizationKey) = "") {
		Do ..GenerateAuthKey()
	}
	quit ^%WebTerminal.Engine.AuthorizationKey
]]></Implementation>
</Method>

<Method name="SendData">
<Description>
Function sends data derectly to server with specified action</Description>
<FormalSpec>query:%String="",action:%String=""</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	if (action = "") { set action = ..ConstClientOutputMessage }
	do ..Write(action _ query) // using CTWPv3
]]></Implementation>
</Method>

<Method name="ExecuteSQL">
<FormalSpec>query:%String=""</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set io = $io
  	use io::("^" _ ..InitialZName)
  	
  	do ..StartClearIO()
 	do ##class(%Device).ReDirectIO($$$YES)
	
  	set tStatement = ##class(%SQL.Statement).%New()
  	set qStatus = tStatement.%Prepare(query)
  	if qStatus'=1 {
	  	write "SQL prepare error: ",$System.Status.DisplayError(qStatus)
	} else {
		set rset = tStatement.%Execute()
  		do rset.%Display()
	}
	
	do ##class(%Device).ReDirectIO($$$NO)
	do ..EndClearIO()
	
	use io
	
	quit $$$OK
]]></Implementation>
</Method>

<Method name="ClientAlertNamespace">
<Description>
Sends to client new namespace if last was changed</Description>
<Implementation><![CDATA[
	if (..CurrentNamespace '= $Znspace) { // change client namespace
	    set ..CurrentNamespace = $Znspace
	 	do ..SendData(..CurrentNamespace,..ConstClientChangeNamespace) 
    }
]]></Implementation>
</Method>

<Method name="StartClearIO">
<Description>
Starts clear I/O mode</Description>
<Implementation><![CDATA[	do ..Write(..ConstClientEnterClearIO)
]]></Implementation>
</Method>

<Method name="EndClearIO">
<Description>
Ends clear I/O mode</Description>
<Implementation><![CDATA[	do ..SendData("exit",..ConstClientExitClearIO)
]]></Implementation>
</Method>

<Method name="ExecuteCommand">
<Description>
The heart of terminal application</Description>
<FormalSpec>query:%String=""</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	if (query = "") quit $$$OK
	
	#dim exception as %Exception.AbstractException
	set value = $Replace(query,$CHAR(10)," ")
	
	do ..StartClearIO() // execute session start
	
	set $ZERROR = ""
	set io = $io
  	use io::("^" _ ..InitialZName)
  	USE io:(::"+T")
  	
 	do ##class(%Device).ReDirectIO($$$YES)
    try { xecute value } catch exception {
	   	set $ZERROR = exception.DisplayString()
	}
	do ##class(%Device).ReDirectIO($$$NO)
	
    use io:(::"-T")
   	if ($ZERROR '= "") {
		do ..SendData($ZERROR)
   	}
   	
   	do ..EndClearIO() // execute session end
	do ..ClientAlertNamespace()
	
	quit $$$OK
]]></Implementation>
</Method>

<Method name="RequireAuthorization">
<Description>
This method holds process and expects only one package from
the client - it includes authorization key.</Description>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set key = ..Read(,.status) // wait for package
	set realKey = ..GetAuthKey() // remember current auth key
	do ..GenerateAuthKey() // generate new auth key
	
	if (key = realKey) {
		quit $$$OK	
	}
	
	quit $$$NOTOK
]]></Implementation>
</Method>

<Method name="ClientLoop">
<Description><![CDATA[
Main method for every new client.

Authorization process description:
	1.	Client requests cache server page called "WebTerminal.csp";
	2.	Server return this page with ClassMethod GetAuthKey() value;
	3.	After client receives page, it performs websocket connection;
		to same web-resource, but with next URL changes:
		"http://" -> "ws://", "WebTerminal.csp" -> "Webterminal.Engine.cls";
	4.	After connection established, client sends his AuthKey to server
		and removes it locally (because it needed only one time);
	5.	Server compares again received key with ..GetAuthKey() value and
		grants permission, if keys are equal.
	>>	As a result - there is no way to autorize via websocket without
		accessing WebTerminal.csp.

Method description:
	First, method requires from client 
	a key, which will be compared with ..GetAuthKey() (global). If 
	succeeded, method sets "authorized" flag to true and changes global
	key with ..GenerateAuthKey() for the next client.]]></Description>
<FormalSpec>clientID:%Numeric</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	
	kill // clear junk vars
	
	for {
		
		set data = ..Read(,.status)
    
    	if $$$ISERR(status) {
	    
	    	do ..Debug($$$CSPWebSocketClosed)
	    	do ..SendData("Error occured while reading data.",
	    		..ConstClientOutputMessage)
      		quit:($$$GETERRORCODE(status)=$$$CSPWebSocketClosed)
	    	
	   	}
	   	
	   	set action = $EXTRACT(data, 1, 1)
	   	set data = $EXTRACT(data, 2, *)
	    		    	
	    if (action = ..ConstServerActionExecute) { // autorized
	    	
		    do ..ExecuteCommand(data)
		    	
	    } elseif (action = ..ConstServerActionExecuteSQL) { // sql
	    
		    do ..ExecuteSQL(data)
		  
	    } elseif (action = ..ConstServerActionGenerateAutocomplete) {
		      
		    do ..GenerateAutocompleteFile(data)
		    		
	    } else { // something scary
		    	
		    do ..SendData("Unrecognised action: " _ action)
		    		
	    }
      		
	}
]]></Implementation>
</Method>

<Method name="Server">
<Description>
New connection established</Description>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	
	set ..lastClientId = ..lastClientId + 1
	
	if ($$$ISOK(..RequireAuthorization())) {
		
		set ..InitialZName = $Zname
		
		do ..SendData("Terminal remote ready. Welcome.")
		
		zn "%SYS"
		set ..CurrentNamespace = $Znspace
		
		do ..SendData(..CurrentNamespace,..ConstClientChangeNamespace)
		   
		do ..ClientLoop(..lastClientId)
		
	} else {
		
		do ..SendData("Welcome, mr. Hacker!")
		do ..EndServer()
			
	}
	
	do ..SendData("Terminal session ended. Goodbye.")
  	
  	quit $$$OK
]]></Implementation>
</Method>
</Class>
</Export>
